create or replace package body PackageTest2 as
PROCEDURE INS_CABECERADETALLE(P_STRXML IN CLOB, P_TRANSACCION_ID OUT NUMBER)
  AS
    V_XMLSOURCE XMLType := XMLType(P_STRXML);
    V_TRANSACCION_ID NUMBER;
    
    --CAMPOS CABECERA
    V_FECHA VARCHAR2(10);
    
    --CAMPOS DETALLE
    TYPE DETALLE_TABLE IS TABLE OF DETALLE%ROWTYPE;
    DETALLE_LIST DETALLE_TABLE;
  BEGIN
    --1. EXTRAER CABECERA
    SELECT EXTRACTVALUE(V_XMLSOURCE, '*/Fecha') INTO V_FECHA from dual;
    
    --2. INSERTAR CABECERA
    INSERT INTO CABECERA(ID,FECHA) 
      VALUES(CABECERA_ID_SEQ.NEXTVAL, V_FECHA) RETURNING ID INTO V_TRANSACCION_ID;
    
    --3. EXTRAER DETALLE
    SELECT EXTRACTVALUE(VALUE(dataXml), '*/Id') AS Id
        ,EXTRACTVALUE(VALUE(dataXml), '*/ProductId') AS ProductId
        ,V_TRANSACCION_ID AS IdCabecera
    BULK COLLECT
    INTO DETALLE_LIST
    FROM TABLE(XMLSEQUENCE(EXTRACT(V_XMLSOURCE, '*/Details/OrderDetail'))) dataXml;
    
    --4. INSERTAR DETALLE
    FORALL i in DETALLE_LIST.FIRST .. DETALLE_LIST.LAST
      INSERT INTO DETALLE VALUES DETALLE_LIST(i);
    
    --5. RETORNAR EL ID CABECERA
    P_TRANSACCION_ID := V_TRANSACCION_ID;
  END INS_CABECERADETALLE; 
end PackageTest2;